int iButton

int iVendors
int iActors
ref rActor
array_var aForE
string_var sMsgBox

begin Function {iButton}

	iVendors = Goo1.AuxVarGetFlt "*PVR_Flt" 1

	; Add vendor selected
	if iButton == 0

		; Build message box of nearby vendors available to add
		sMsgBox = "^Vendor Radio^"
		Goo1.AuxVarErase "*PVR_Add"
		foreach aForE <- (GetActorsByProcessingLevel 0)
			rActor = *aForE
			if eval rActor.GetMerchantContainer && rActor.IsPersistent && rActor.GetTalkedToPC && GetFormRefs (rActor.GBO) && (HasKeyword rActor "VendorRadio" == 0 || rActor.AuxVarGetFlt "*VendorRadio" == 1)
				Goo1.AuxVarSetRef "*PVR_Add" rActor -1
				sMsgBox += ("|" + rActor.LNGetName)
				iActors += 1
			endif
		loop
		if iActors > 0
			sMsgBox += "|Cancel"
		else
			sMsgBox += "|None Available"
		endif
		Goo1.AuxVarSetFlt "*PVR_Flt" iActors 1
		MessageBoxExAlt (CompileScript "VendorRadio\KeywordAdd.gek") "%z" sMsgBox

		aForE = Ar_Null
		Sv_Destruct sMsgBox

	; Remove vendor selected
	elseif iVendors > 0 && iButton == 1
		MessageBoxExAlt (CompileScript "VendorRadio\KeywordRemove.gek") "%z|Cancel" (Goo1.AuxVarGetStr "*PVR_Str" 0)

	; Return to main menu
	else
		Call (CompileScript "VendorRadio\MsgInit.gek")
	endif

end