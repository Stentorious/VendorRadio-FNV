int iButton

int iVendors
int iExpBoxes
ref rExpBox
array_var aMEBRefs
array_var aMEBNames
array_var aForE
string_var sMsgBox

begin Function {iButton}

	iVendors = Goo1.AuxVarGetFlt "*PVR_Flt" 1

	; Vendor selected
	if iButton < iVendors
		Goo1.AuxVarSetRef "*PVR_Vend" (Goo1.AuxVarGetRef "*PVR_Ven" iButton)

		; Shipping enabled
		if Goo1.AuxVarGetFlt "*PVR_INI" 1 == 1
			sMsgBox = "^Vendor Radio^Shipping Location:"

			; Express box arrays
			if eval IsModLoaded "TaleOfTwoWastelands.esm"
				aMEBRefs = Ar_List vMEPrimmRef1, vMENovacRef, vMEVegasRef, vMEGoodSpringsRef, vMEFreesideRef, (GFFM "TaleOfTwoWastelands.esm" "8F78"), (GFFM "TaleOfTwoWastelands.esm" "8F7B"), (GFFM "TaleOfTwoWastelands.esm" "8F7A"), (GFFM "TaleOfTwoWastelands.esm" "8F79"), (GFFM "TaleOfTwoWastelands.esm" "8F77")
				aMEBNames = Ar_List "Primm", "Novac", "New Vegas Strip", "Goodsprings", "Freeside", "Paradise Falls", "Megaton", "Canterbury Commons", "Tenpenny Tower", "Rivet City"
			else
				aMEBRefs = Ar_List vMEPrimmRef1, vMENovacRef, vMEVegasRef, vMEGoodSpringsRef, vMEFreesideRef
				aMEBNames = Ar_List "Primm", "Novac", "New Vegas Strip", "Goodsprings", "Freeside"
			endif

			; Build message box displaying all available express boxes
			Goo1.AuxVarErase "*PVR_MEB"
			Goo1.AuxVarErase "*PVR_MEB_Str"
			foreach aForE <- aMEBRefs
				rExpBox = *aForE
				if rExpBox.GetVariable "bDiscovered"
					iExpBoxes += 1
					Goo1.AuxVarSetRef "*PVR_MEB" rExpBox -1
					Goo1.AuxVarSetStr "*PVR_MEB_Str" (aMEBNames[aForE["key"]]) -1
					sMsgBox += ("|" + aMEBNames[aForE["key"]])
				endif
			loop
			if iExpBoxes > 0
				sMsgBox += "|Cancel"
			else
				sMsgBox += "|None Available"
			endif

			Goo1.AuxVarSetFlt "*PVR_Flt" iExpBoxes 2

			MessageBoxExAlt (CompileScript "VendorRadio\MsgShipping.gek") "%z" sMsgBox

			aMEBRefs = aMEBNames = aForE = Ar_Null

		; Direct vendor access enabled
		else
			Call (CompileScript "VendorRadio\Barter.gek")
		endif

	; Add vendor option selected
	elseif iButton == iVendors
		if iVendors > 0
			sMsgBox = "^Vendor Radio^|Add Vendor|Remove Vendor|Cancel"
		else
			sMsgBox = "^Vendor Radio^|Add Vendor|Cancel"
		endif
		MessageBoxExAlt (CompileScript "VendorRadio\Settings.gek") "%z" sMsgBox
	endif

	Sv_Destruct sMsgBox

end